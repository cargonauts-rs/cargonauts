use heck::KebabCase;

use ast::*;

grammar;

pub routes: Routes = {
    <asset_handler:AssetHandler?> <setup:Setup?> <resources:Resource*>
        => Routes { asset_handler, setup, resources },
};

AssetHandler: String = {
    "use" <Ident> "for" "assets" ";" => <>,
};

Setup: Setup = {
    "setup" "{" <members:SetupMember*> "}" => Setup { members, },
};

SetupMember: SetupMember = {
    "connection" <Connection> => SetupMember::Connection(<>),
};

Connection: Connection = {
    "to" <conn:Ident> ";" => Connection { name: conn.to_kebab_case(), conn },
    "to" <conn:Ident> "as" <name:String> ";" => Connection { conn, name },
};

Resource: Resource = {
    <attrs:Attribute*> <header:ResourceHeader> ";" => Resource {
        attrs, header, members: vec![],
    },
    <attrs:Attribute*> <header:ResourceHeader> "{" <members:ResourceMember*> "}" => Resource {
        attrs, header, members,
    },
};

ResourceHeader: ResourceHeader  = {
    "resource" <ty:Ident> <endpoint:Endpoint?> => ResourceHeader { ty, endpoint },
};

Endpoint: String = "@" <String>;

ResourceMember: ResourceMember = {
    "relation" <Relation> => ResourceMember::Relation(<>),
    "method" <Method> => ResourceMember::Method(<>),
};

Relation: Relation = {
    <attrs:Attribute*> <rel:Ident> <endpoint:Endpoint?> ";" => Relation {
        attrs, rel, endpoint, members: vec![],
    },
    <attrs:Attribute*> <rel:Ident> <endpoint:Endpoint?> "{" <members:RelationMember*> "}" => Relation {
        attrs, rel, endpoint, members,
    },
};

RelationMember: RelationMember = {
    "method" <Method> => RelationMember::Method(<>),
};

Method: Method = {
    <attrs:Attribute*> <method:Ident> "in" <format:Ident> ";" => Method {
        attrs, method, format,
    }
};

Ident: String = {
    r"[A-Za-z][A-Za-z0-9_]*" => String::from(<>)
};

String: String = {
    <s:r#""(?:[^"\\]|\\.)*""#> => {
        let last = s.len() - 1;
        String::from(&s[1..last])
    },
};

Attribute: Attribute = {
    "#" "[" <id: Ident> "(" <idents:RepeatWith<Ident, ",">> ")" "]"
        => Attribute::Arg(id, idents),
    "#" "[" <Ident> "]"
        => Attribute::Ident(<>),
};

RepeatWith<T, X>: Vec<T> = {
    <v:(<T> X)*> <e:T?> => match e {
       None => v,
       Some(e) => {
           let mut v = v;
           v.push(e);
           v
       }
   }
};
